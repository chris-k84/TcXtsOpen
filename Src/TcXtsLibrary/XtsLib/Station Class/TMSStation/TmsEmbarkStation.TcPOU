<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="TmsEmbarkStation" Id="{044d9b9f-48a1-452d-b127-28199a3c04bb}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TmsEmbarkStation EXTENDS Process IMPLEMENTS ITF_SwitchStation
VAR_INPUT
	StateMachine : IPmlStateMachine;
	TmsSwitch : ITF_TmsSwitch;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	DepartMover : ITmsMover;
	_stationPosition : INT := 1; //1 is low, 2 is high
	State :(
		Init,                                               
		Ready,                                 
        Waiting,
		HasSwitch,
		Call,
		Processing,
		Boarding,
		BoardingComplete,
		Moving); 
	_switchPosition: MC_LREAL;
	demandPosition : TmsSwitchPosition;
	arrTrackIds : ARRAY[1..4] OF OTCID := [16#01010100,16#010100F0,16#010100E0,16#01010790];
	isUpAllowed : BOOL := TRUE;
	isDownAllowed : BOOL := FALSE;
	isMiddleAllowed : BOOL := TRUE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE State OF
	Init:
		_Operating := _Clear := _Complete := _Ready := _HasMover := FALSE;
		_Positions[1] := _Positions[2] := _Positions[3] := _Positions[4] := 0;
		NumberOfPositions := 0;
		AssignedPositions := 1;
		StationMovers := 0;
		State := Ready;
	
	Ready:	
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Clearing THEN
			_Operating := _Complete := _HasMover := FALSE;
			_Clear := _Ready := TRUE;
			StationMovers := 0;
			State := Waiting;
		END_IF
	
	Waiting:
		IF _HasMover THEN
			State := HasSwitch;
			_Operating := TRUE;
			_Clear := FALSE;
			_Ready := FALSE;
			_Complete := FALSE;
		END_IF
	
	HasSwitch:
		IF (_stationPosition = 1 AND (TmsSwitch.Switch = TmsSwitchPosition.Down OR TmsSwitch.Switch = TmsSwitchPosition.Middle))
			OR (_stationPosition = 2 AND (TmsSwitch.Switch = TmsSwitchPosition.Up OR TmsSwitch.Switch = TmsSwitchPosition.Middle)) THEN
				IF TmsSwitch.ExchangeMover(Mover := DepartMover, currentStation := THIS^) THEN
					State := Processing;
				END_IF
		ELSE
			State := Call;
		END_IF	
		
	Call:
		IF NOT TmsSwitch.HasMover() AND TmsSwitch.IsStationary() THEN
			IF TmsSwitch.MoveTo(positionState := TmsSwitchPosition.Middle) THEN
				State := HasSwitch;
			END_IF
		ELSE
			State := Waiting;
		END_IF
	
	Processing:
		IF _stationPosition = 1 THEN
			IF TmsSwitch.Switch = TmsSwitchPosition.Down and isMiddleAllowed THEN
				IF DepartMover.ActiveTrack = arrTrackIds[4] THEN
					State := Boarding;
				ELSE
					DepartMover.ActivateTrack(arrTrackIds[4]);
					State := Boarding;
				END_IF
				demandPosition := TmsSwitchPosition.Middle;
			ELSIF TmsSwitch.Switch = TmsSwitchPosition.Middle AND isUpAllowed THEN
				IF DepartMover.ActiveTrack = arrTrackIds[1] THEN
					State := Boarding;
				ELSE
					DepartMover.ActivateTrack(arrTrackIds[1]);
					State := Boarding;
				END_IF
				demandPosition := TmsSwitchPosition.Up;
			END_IF
		ELSE
			IF TmsSwitch.Switch = TmsSwitchPosition.Up AND isMiddleAllowed THEN
				IF DepartMover.ActiveTrack = arrTrackIds[3] THEN
					State := Boarding;
				ELSE
					DepartMover.ActivateTrack(arrTrackIds[3]);
					State := Boarding;
				END_IF
				demandPosition := TmsSwitchPosition.Middle;
			ELSIF TmsSwitch.Switch = TmsSwitchPosition.Middle AND isDownAllowed THEN
				IF DepartMover.ActiveTrack = arrTrackIds[2] THEN
					State := Boarding;
				ELSE
					DepartMover.ActivateTrack(arrTrackIds[2]);
					State := Boarding;
				END_IF
				demandPosition := TmsSwitchPosition.Down;
			END_IF
		END_IF
		
	Boarding:
		DepartMover.MoveTo(Position := _switchPosition, Velocity := 100, BackwardMove := FALSE);
		IF DepartMover.isAtPosition(position := _switchPosition) THEN
			State := BoardingComplete;
		END_IF
		
	BoardingComplete:
		IF TmsSwitch.MoveTo(positionState := demandPosition) THEN
			State := Moving;
		END_IF
		
	Moving:
		IF _Complete AND NOT _HasMover THEN
			_Operating := FALSE;
			_Clear := TRUE;
			_Ready := TRUE;
			State := Waiting;
		END_IF		
END_CASE]]></ST>
    </Implementation>
    <Method Name="HasMover" Id="{6096dfca-b0c2-4998-b7bb-2c09860d3176}">
      <Declaration><![CDATA[METHOD HasMover : BOOL
VAR_INPUT
	mover : IMover;
END_VAR
VAR
	iTmsMover : ITmsMover;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF StationMovers <= 2 THEN
	StationMovers := StationMovers + 1;
	IF __QUERYINTERFACE(mover,iTmsMover) THEN
		DepartMover := iTmsMover;
		_HasMover := true;
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="MoverAway" Id="{5d30fab5-165d-4159-bdc5-36df29db049b}">
      <Declaration><![CDATA[METHOD PUBLIC MoverAway : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[StationMovers := StationMovers - 1;
IF StationMovers = 0 THEN
	_HasMover := FALSE;
END_IF
DepartMover := 0;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="StationPosition" Id="{04b01043-8abc-480b-b7a1-3b47b2f6b3d3}">
      <Declaration><![CDATA[PROPERTY StationPosition : INT
]]></Declaration>
      <Get Name="Get" Id="{63f29fcb-9246-41b7-8237-0e14a4d877b8}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[StationPosition := _stationPosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{8dad77b7-b3d3-4e11-af0a-1ace0eb16549}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_stationPosition := StationPosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="SwitchMoveComplete" Id="{b6e4018d-1b11-4436-abf3-c2d1900106a2}">
      <Declaration><![CDATA[METHOD PUBLIC SwitchMoveComplete : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_Complete := SwitchMoveComplete := true;]]></ST>
      </Implementation>
    </Method>
    <Property Name="SwitchTravelPosition" Id="{77eb7d82-50db-4968-86fb-d2f009644fbf}">
      <Declaration><![CDATA[PROPERTY SwitchTravelPosition : mc_lreal
]]></Declaration>
      <Get Name="Get" Id="{5e9136b4-3bc6-41b5-a233-22f210e01c6b}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[SwitchTravelPosition := _switchPosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{58d1b30d-be8b-4b08-b209-84e887ca28ad}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_switchPosition := SwitchTravelPosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="TmsEmbarkStation">
      <LineId Id="886" Count="101" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.HasMover">
      <LineId Id="9" Count="3" />
      <LineId Id="20" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.MoverAway">
      <LineId Id="31" Count="0" />
      <LineId Id="35" Count="2" />
      <LineId Id="11" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.StationPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.StationPosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.SwitchMoveComplete">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.SwitchTravelPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="TmsEmbarkStation.SwitchTravelPosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>