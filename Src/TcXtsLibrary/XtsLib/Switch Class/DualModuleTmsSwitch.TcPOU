<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="DualModuleTmsSwitch" Id="{277d4c84-a32d-4184-90e2-a93e1f285faf}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK DualModuleTmsSwitch IMPLEMENTS ITF_TmsSwitch
VAR_INPUT
	StateMachine : IPmlStateMachine;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	_exchangeMover : IMover;
	_currentStation : ITF_SwitchStation;
	_demandPosition : MC_LREAL;
	_upPosition : MC_LREAL;
	_middlePosition : MC_LREAL;
	_downPosition : MC_LREAL;
	SwitchVelocity : MC_LREAL := 100.0;
	switchAxis : MC2_Axis_Base;
	arrTrackIds : ARRAY[1..4] OF OTCID := [16#01010100,16#010100F0,16#010100E0,16#01010790];
	stSwitchInfo : TmsSwitchInfo;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE stSwitchInfo.State OF
	TmsSwitchState.Init:
		IF NOT switchAxis.hasError() THEN
			stSwitchInfo.State := TmsSwitchState.Ready;
		END_IF
		
	TmsSwitchState.Ready:
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Clearing THEN
			switchAxis.EnableAxis();
			stSwitchInfo.State := TmsSwitchState.Enabling;
		END_IF	
		
	TmsSwitchState.Enabling:
		IF switchAxis.isEnabled() THEN
			stSwitchInfo.State := TmsSwitchState.Enabled;
		END_IF
		
	TmsSwitchState.Enabled:
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Resetting THEN
			IF switchAxis.MoveTo(Position := _middlePosition, Velocity := 100.0) OR switchAxis.isAtPosition(position := _middlePosition) THEN
				stSwitchInfo.State := TmsSwitchState.Resetting;
			END_IF
		END_IF
		
	TmsSwitchState.Resetting:
		IF switchAxis.isAtPosition(position := MiddlePosition) THEN
			stSwitchInfo.State := TmsSwitchState.Waiting;
		END_IF	
	
	TmsSwitchState.Waiting:
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Aborting THEN
			switchAxis.DisableAxis();
			stSwitchInfo.State := TmsSwitchState.Disabling;
		END_IF
		
	TmsSwitchState.AssignMover:
		IF HasMover() THEN
			IF FindNewTrack() <> 0 THEN
				stSwitchInfo.State := TmsSwitchState.Moving;
			END_IF
		ELSE
			stSwitchInfo.State := TmsSwitchState.Moving;
		END_IF

	TmsSwitchState.Moving:	
		IF switchAxis.isAtPosition(_demandPosition) THEN
			stSwitchInfo.State := TmsSwitchState.Done;
		END_IF
	
	TmsSwitchState.Done:
		IF _currentStation <> 0 THEN
			IF _currentStation.SwitchMoveComplete() THEN
				_exchangeMover := 0;
				_currentStation := 0;
			END_IF	
		END_IF
		stSwitchInfo.State := TmsSwitchState.Waiting;

	TmsSwitchState.Disabling:
		IF NOT switchAxis.isEnabled() THEN
			stSwitchInfo.State := TmsSwitchState.Init;
		END_IF

END_CASE

switchAxis();

//Cyclic get switch info
stSwitchInfo.ActPos := switchAxis._axis.NcToPlc.ActPos;
stSwitchInfo.Position := Switch;]]></ST>
    </Implementation>
    <Property Name="DownPosition" Id="{e0b661ce-ed86-4d1f-9222-494e093bbcab}">
      <Declaration><![CDATA[PROPERTY DownPosition : MC_LREAL]]></Declaration>
      <Get Name="Get" Id="{6539c5f6-ea7d-4749-9266-3355a5abbe04}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[DownPosition := _downPosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{456f8e9b-fb92-4f46-ad80-573ff9f45703}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_downPosition := DownPosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="ExchangeMover" Id="{91294689-d92f-47b9-bf3f-83179151544e}">
      <Declaration><![CDATA[METHOD PUBLIC ExchangeMover : BOOL
VAR_INPUT
	Mover : IMover;
	CurrentStation : ITF_SwitchStation;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _exchangeMover = 0 AND State = TmsSwitchState.Waiting AND NOT (stSwitchInfo.Mode = TmsSwitchMode.Manual) THEN
	_exchangeMover := Mover;	
	_currentStation := CurrentStation;
	ExchangeMover := TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FindNewTrack" Id="{5617b4a9-1422-4e43-b3c2-f14d4e729a12}">
      <Declaration><![CDATA[METHOD PUBLIC FindNewTrack : OTCID
VAR_INPUT
END_VAR
VAR
	iTmsMover : ITmsMover;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __QUERYINTERFACE(_exchangeMover,iTmsMover) THEN
	IF Switch = TmsSwitchPosition.Up THEN
		IF iTmsMover.ActiveTrack = arrTrackIds[3] THEN
			iTmsMover.ActivateTrack(newTrack := arrTrackIds[1]);
		END_IF
	ELSIF Switch = TmsSwitchPosition.Middle THEN
		IF iTmsMover.ActiveTrack = arrTrackIds[1] THEN
			iTmsMover.ActivateTrack(newTrack := arrTrackIds[3]);
		ELSIF iTmsMover.ActiveTrack = arrTrackIds[2] THEN
			iTmsMover.ActivateTrack(newTrack := arrTrackIds[4]);
		END_IF
	ELSE
		IF iTmsMover.ActiveTrack = arrTrackIds[4] THEN
			iTmsMover.ActivateTrack(newTrack := arrTrackIds[2]);	
		END_IF
	END_IF
END_IF
FindNewTrack := iTmsMover.ActiveTrack;]]></ST>
      </Implementation>
    </Method>
    <Method Name="HasMover" Id="{95685ce2-b76a-43e9-93b4-0b63d3740469}">
      <Declaration><![CDATA[METHOD HasMover : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[HasMover := _exchangeMover <> 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsStationary" Id="{676afb09-a46e-4c6e-a2ab-020bf1490f01}">
      <Declaration><![CDATA[METHOD IsStationary : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsStationary := switchAxis.isStationary();]]></ST>
      </Implementation>
    </Method>
    <Property Name="MiddlePosition" Id="{d8d7aa8a-1259-4e0d-918b-13e95c3f06b3}">
      <Declaration><![CDATA[PROPERTY PUBLIC MiddlePosition : MC_LREAL]]></Declaration>
      <Get Name="Get" Id="{cb254194-ced1-415a-b9f3-61f227455e74}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[MiddlePosition := _middlePosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{be9ba341-3559-498f-b610-9f2f1e919e44}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_middlePosition := MiddlePosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="MoveTo" Id="{14737c8e-a13f-4ad0-b4be-4cc0c0734079}">
      <Declaration><![CDATA[METHOD PUBLIC MoveTo : BOOL
VAR_INPUT
	positionState : TmsSwitchPosition;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE positionState OF
	TmsSwitchPosition.Down:
		IF NOT switchAxis.isAtPosition(position := _downPosition) THEN
			IF switchAxis.MoveTo(Position := _downPosition, Velocity := SwitchVelocity) THEN
				MoveTo := TRUE;
				stSwitchInfo.State := TmsSwitchState.AssignMover;
				_demandPosition := _downPosition;
			END_IF
		END_IF	
	TmsSwitchPosition.Middle:
		IF NOT switchAxis.isAtPosition(position := _middlePosition) THEN
			IF switchAxis.MoveTo(Position := _middlePosition, Velocity := SwitchVelocity) THEN
				MoveTo := TRUE;
				stSwitchInfo.State := TmsSwitchState.AssignMover;
				_demandPosition := _middlePosition;
			END_IF
		END_IF	
	TmsSwitchPosition.Up:
		IF NOT switchAxis.isAtPosition(position := _upPosition) THEN
			IF switchAxis.MoveTo(Position := _upPosition, Velocity := SwitchVelocity) THEN
				MoveTo := TRUE;
				stSwitchInfo.State := TmsSwitchState.AssignMover;
				_demandPosition := _upPosition;
			END_IF
		END_IF	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Property Name="Switch" Id="{8f864557-44eb-4f80-a745-5cd005f038e1}">
      <Declaration><![CDATA[PROPERTY PUBLIC Switch : TmsSwitchPosition]]></Declaration>
      <Get Name="Get" Id="{aede540a-2b76-416f-bd6c-7ccdd9753ba7}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF switchAxis.isAtPosition(position := _upPosition) THEN
	Switch := TmsSwitchPosition.Up;
ELSIF switchAxis.isAtPosition(position := _middlePosition) THEN
	Switch := TmsSwitchPosition.Middle;
ELSIF switchAxis.isAtPosition(position := _downPosition) THEN
	Switch := TmsSwitchPosition.Down;
ELSE
	Switch := TmsSwitchPosition.Moving;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="UpperPosition" Id="{80967e98-671b-4a73-822c-40bc0a4eddd0}">
      <Declaration><![CDATA[PROPERTY PUBLIC UpperPosition : MC_LREAL]]></Declaration>
      <Get Name="Get" Id="{142ba753-3293-4702-b2a5-74b9d29694a2}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[UpperPosition := _upPosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{943e6685-87cf-4614-b541-9caf192c9a62}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_upPosition := UpperPosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="DualModuleTmsSwitch">
      <LineId Id="74" Count="1" />
      <LineId Id="80" Count="0" />
      <LineId Id="133" Count="1" />
      <LineId Id="145" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="138" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="141" Count="2" />
      <LineId Id="93" Count="1" />
      <LineId Id="146" Count="1" />
      <LineId Id="149" Count="1" />
      <LineId Id="148" Count="0" />
      <LineId Id="108" Count="1" />
      <LineId Id="118" Count="0" />
      <LineId Id="152" Count="1" />
      <LineId Id="151" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="309" Count="1" />
      <LineId Id="314" Count="0" />
      <LineId Id="308" Count="0" />
      <LineId Id="200" Count="1" />
      <LineId Id="457" Count="1" />
      <LineId Id="461" Count="1" />
      <LineId Id="533" Count="1" />
      <LineId Id="459" Count="0" />
      <LineId Id="237" Count="0" />
      <LineId Id="239" Count="0" />
      <LineId Id="244" Count="2" />
      <LineId Id="240" Count="1" />
      <LineId Id="465" Count="0" />
      <LineId Id="468" Count="0" />
      <LineId Id="470" Count="2" />
      <LineId Id="467" Count="0" />
      <LineId Id="474" Count="0" />
      <LineId Id="318" Count="0" />
      <LineId Id="238" Count="0" />
      <LineId Id="315" Count="2" />
      <LineId Id="132" Count="0" />
      <LineId Id="62" Count="1" />
      <LineId Id="30" Count="0" />
      <LineId Id="569" Count="0" />
      <LineId Id="568" Count="0" />
      <LineId Id="566" Count="1" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.DownPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.DownPosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.ExchangeMover">
      <LineId Id="7" Count="1" />
      <LineId Id="11" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.FindNewTrack">
      <LineId Id="40" Count="16" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.HasMover">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.IsStationary">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.MiddlePosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.MiddlePosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.MoveTo">
      <LineId Id="140" Count="24" />
      <LineId Id="81" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.Switch.Get">
      <LineId Id="21" Count="7" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.UpperPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="DualModuleTmsSwitch.UpperPosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>