<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="XtsTrack" Id="{9b564055-e4ef-49ec-91fa-4e16cb16a3f0}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK XtsTrack IMPLEMENTS IXtsTrack
VAR_INPUT
	StateMachine : IPmlStateMachine;
	TmsSwitch : ITF_TmsSwitch;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	_trackLength : MC_LREAL;
	_homePosition : MC_LREAL;
	HomeSpeed : MC_LREAL := 1000.0;
	mcCollisionGroup : CollisionGroup;
	XtsTrackHardware : XtsHardware;
	EnableSequence : XtsEnableSequence(mcCollisionGroup);
	DisableSequence : XtsDisableSequence(mcCollisionGroup);
	
	State :(
        Init,                                               
		Ready,                                 
        Enabling,
		Enabled,
		Resetting,
		Running,
		Disabling);  	
	i : DINT;
	AtHome: BOOL;
	_tracks : INT;
END_VAR
VAR_IN_OUT
	MoverArray : ARRAY[*] OF IMover;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[XtsTrackHardware(Tracks := _tracks, TmsSwitch := TmsSwitch);
CASE State OF
	Init:
		IF XtsTrackHardware.XtsHardwareOK THEN
			AssignMoverToTrack();
			State := Ready;
		END_IF
		
	Ready:
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Clearing THEN
			IF EnableSequence.Enable() THEN
				State := Enabling;
			END_IF	
		END_IF
		
	Enabling:
		IF EnableSequence.Done THEN
			State := Enabled;
			StateMachine.StateComplete();
		END_IF
		
	Enabled:
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Aborting THEN
			DisableSequence.Disable();
			IF DisableSequence.Busy THEN
				State := Disabling;
			END_IF
		END_IF
		
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Resetting THEN
			FOR i := LOWER_BOUND(MoverArray,1) TO UPPER_BOUND(MoverArray,1) DO
				MoverArray[i].MoveTo(Position := _homePosition, Velocity := HomeSpeed, BackwardMove := FALSE);
			END_FOR 
			State := Resetting;
		END_IF
		
	Resetting:
		AtHome := TRUE;
		FOR i := LOWER_BOUND(MoverArray,1) TO UPPER_BOUND(MoverArray,1) DO
				AtHome := AtHome AND MoverArray[i].isStationary();
		END_FOR 
		IF AtHome THEN
			State := Running;
			StateMachine.StateComplete();	
		END_IF
		
	Running:	
		IF StateMachine.GetCurrentState() = E_PMLState.ePMLState_Aborting THEN
			DisableSequence.Disable();
			IF DisableSequence.Busy THEN
				State := Disabling;
			END_IF
		END_IF
		
	Disabling:
		IF DisableSequence.Done THEN
			State := Ready;
			StateMachine.StateComplete();
		END_IF
	
END_CASE

EnableSequence(arrMovers := MoverArray);
DisableSequence(arrMovers := MoverArray);
mcCollisionGroup();]]></ST>
    </Implementation>
    <Method Name="AssignMoverToTrack" Id="{9ccda3a4-60d1-4f4d-8563-7a430a9cb080}">
      <Declaration><![CDATA[METHOD PUBLIC AssignMoverToTrack : BOOL
VAR_INPUT
END_VAR
VAR
	iTmsMover : ITmsMover;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO UPPER_BOUND(MoverArray,1) DO
	IF __QUERYINTERFACE(MoverArray[i],iTmsMover) THEN
		iTmsMover.SetTrackId(XtsTrackHardware.GetMoverTrackId(DINT_TO_INT(i)));
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Property Name="HomePosition" Id="{17d8d860-04e6-4fdc-9d29-4118d8eda955}">
      <Declaration><![CDATA[PROPERTY PUBLIC HomePosition : mc_lreal]]></Declaration>
      <Get Name="Get" Id="{620c6b86-3832-4e3f-9912-ef7e2a1a6514}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[HomePosition := _homePosition;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{f190637e-4d36-4143-baa6-9db775f41566}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_homePosition := HomePosition;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="TrackLength" Id="{4f244eb1-0239-423a-8574-4ccb93081483}">
      <Declaration><![CDATA[PROPERTY PUBLIC TrackLength : mc_lreal]]></Declaration>
      <Get Name="Get" Id="{962af0e5-68fc-457e-92c4-2ae643ddac5e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[TrackLength := _trackLength;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{9d3b1cf5-ae4d-4af9-ab8c-ba44e55a4cc2}">
        <Declaration><![CDATA[PUBLIC 
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_trackLength := TrackLength;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="Tracks" Id="{7741ef95-8846-4cc0-aa81-d8ac8ebba247}">
      <Declaration><![CDATA[PROPERTY PUBLIC Tracks : INT]]></Declaration>
      <Get Name="Get" Id="{d2af4e1e-cc7b-4b07-b5c4-6cf6f6dda28f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Tracks := _tracks;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{bb7806ec-85ad-4f70-9606-95b8e7565462}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_tracks := Tracks;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="XtsTrack">
      <LineId Id="246" Count="3" />
      <LineId Id="339" Count="0" />
      <LineId Id="250" Count="58" />
      <LineId Id="78" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.AssignMoverToTrack">
      <LineId Id="5" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.HomePosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.HomePosition.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.TrackLength.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.TrackLength.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.Tracks.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="XtsTrack.Tracks.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>