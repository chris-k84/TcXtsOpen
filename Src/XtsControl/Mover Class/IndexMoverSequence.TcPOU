<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="IndexMoverSequence" Id="{5b1293cd-307b-4dc9-8f5e-57c564c1904c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK IndexMoverSequence
VAR_INPUT
	ThisMover : IMover;
	XtsTrack : IXtsTrack;
	StateMachine : IPmlStateMachine;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	State :(
        Init,                                               
        Available,
		MovingToStart, 
		AtStart,                                 
        MovingToStation,
		QueuedAtStation,
		AtStation,
		AtProcess,
		InProcess,
		StationProcessComplete,
		Aborting);  
	CurrentStation : INT := 1;
END_VAR
VAR_IN_OUT
	StationArray : ARRAY[*] OF IProcess;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE State OF
	Init:
		IF ThisMover.isDetected() THEN
			State := Available;
		END_IF
		
	Available:
		IF StateMachine.getState() = E_PMLState.ePMLState_Starting THEN
			State := MovingToStart;
		END_IF
		
	MovingToStart:
		IF ThisMover.isAtPosition(position := 5500.0) THEN
			State := AtStart;
		END_IF
			
	AtStart:
		IF StationArray[CurrentStation].IsReady() THEN
			IF ThisMover.MoveTo(Position := StationArray[CurrentStation].Position,
								Velocity := 1000,
								BackwardMove := FALSE) THEN
				State :=  MovingToStation;
			END_IF
		END_IF
		
	MovingToStation:
		IF ThisMover.isStationary() THEN 
			IF ThisMover.isAtPosition(StationArray[CurrentStation].Position) OR ThisMover.isAtPosition(StationArray[CurrentStation].StartPosition) THEN
				IF StationArray[CurrentStation].IsProcess THEN
					State := AtProcess;
				ELSE
					State := AtStation;
				END_IF
				StationArray[CurrentStation].HasMover();
			ELSE
				State := QueuedAtStation;
			END_IF
		END_IF
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF
		
	QueuedAtStation:
		IF NOT ThisMover.isStationary() THEN
				State := MovingToStation;
		END_IF
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF

	AtStation:
		IF StationArray[CurrentStation].isComplete() THEN
			State := StationProcessComplete;
		END_IF
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF

	AtProcess:
		ThisMover.MoveTo(Position := StationArray[CurrentStation].EndPosition,
								Velocity := StationArray[CurrentStation].ProcessVelocity,
								BackwardMove := FALSE);
		State := InProcess;
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF
								
	InProcess:
		IF ThisMover.isStationary() THEN 
			IF ThisMover.isAtPosition(StationArray[CurrentStation].EndPosition) THEN
				State := StationProcessComplete;
			END_IF
		END_IF
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF
	
	StationProcessComplete:
		IF CurrentStation = UPPER_BOUND(StationArray,1) THEN
			IF ThisMover.MoveTo(Position := 5500.0,
							Velocity := 1000,
							BackwardMove := FALSE) THEN
				State := MovingToStart;
				CurrentStation := 1; 
			END_IF
		ELSE
			IF StationArray[CurrentStation + 1].IsProcess THEN
				ThisMover.MoveTo(Position := StationArray[CurrentStation + 1].StartPosition,
								Velocity := 4000,
								BackwardMove := FALSE); 
			ELSE
				ThisMover.MoveTo(Position := StationArray[CurrentStation + 1].Position,
								Velocity := 4000,
								BackwardMove := FALSE);
			END_IF
			CurrentStation := CurrentStation + 1;
			State := MovingToStation;
		END_IF
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborting THEN
			State := Aborting;
		END_IF
		
	Aborting:
		IF StateMachine.getState() = E_PMLState.ePMLState_Aborted THEN
			State := Available;
			CurrentStation := 1;
		END_IF
		
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="IndexMoverSequence">
      <LineId Id="650" Count="107" />
      <LineId Id="39" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>