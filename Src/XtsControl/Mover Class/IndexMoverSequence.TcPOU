<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="IndexMoverSequence" Id="{5b1293cd-307b-4dc9-8f5e-57c564c1904c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK IndexMoverSequence
VAR_INPUT
	ThisMover : IMover;
	XtsTrack : IXtsTrack;
	StateMachine : IPmlStateMachine;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	State :(
        Init,                                               
        Available,
		MovingToStart, 
		AtStart,                                 
        MovingToStation,
		QueuedAtStation,
		AtStation);  
	CurrentStation : INT := 1;
END_VAR
VAR_IN_OUT
	StationArray : ARRAY[*] OF IProcess;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[CASE State OF
	Init:
		IF ThisMover.isDetected() THEN
			State := Available;
		END_IF
		
	Available:
		IF StateMachine.getState() = E_PMLState.ePMLState_Starting THEN
			State := MovingToStart;
		END_IF
		
	MovingToStart:
		IF ThisMover.isAtPosition(position := 5500.0) THEN
			State := AtStart;
		END_IF
			
	AtStart:
		IF StationArray[CurrentStation].IsReady() THEN
			IF ThisMover.MoveTo(Position := StationArray[CurrentStation].Position,
								Velocity := 1000,
								BackwardMove := FALSE) THEN
				State :=  MovingToStation;
			END_IF
		END_IF
		
	MovingToStation:
		IF ThisMover.isStationary() THEN 
			IF ThisMover.isAtPosition(StationArray[CurrentStation].Position) THEN
				State := AtStation;
				StationArray[CurrentStation].HasMover();
			ELSE
				State := QueuedAtStation;
			END_IF
		END_IF
		
	QueuedAtStation:
		IF NOT ThisMover.isStationary() THEN
				State := MovingToStation;
		END_IF

	AtStation:
		IF StationArray[CurrentStation].isComplete() THEN
			IF CurrentStation = UPPER_BOUND(StationArray,1) THEN
				IF ThisMover.MoveTo(Position := 5500.0,
								Velocity := 1000,
								BackwardMove := FALSE) THEN
					State := MovingToStart;
					CurrentStation := 1; 
				END_IF
			ELSE
				IF ThisMover.MoveTo(Position := StationArray[CurrentStation + 1].Position,
								Velocity := 1000,
								BackwardMove := FALSE) THEN
					CurrentStation := CurrentStation + 1;
					State := MovingToStation;
				END_IF
			END_IF
		END_IF

END_CASE]]></ST>
    </Implementation>
    <LineIds Name="IndexMoverSequence">
      <LineId Id="304" Count="58" />
      <LineId Id="39" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>